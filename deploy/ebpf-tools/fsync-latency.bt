#!/usr/bin/env bpftrace

BEGIN
{
  @target_pid = $1;
  if (@target_pid == 0) {
    printf("Usage: fsync-latency.bt <PID> [threshold_ms]\n");
    exit();
  }
  @threshold_ms = $2;
  if (@threshold_ms == 0) { @threshold_ms = 5; }
  printf("Tracing fsync latency > %d ms for pid %d... Hit Ctrl-C to stop.\n", @threshold_ms, @target_pid);
}

tracepoint:syscalls:sys_enter_fsync /pid == @target_pid/
{
  @start[tid] = nsecs;
}

tracepoint:syscalls:sys_exit_fsync /@start[tid] && pid == @target_pid/
{
  $delta_ms = (nsecs - @start[tid]) / 1000000;
  if ($delta_ms >= @threshold_ms) {
    printf("fsync %d ms pid=%d comm=%s ret=%d\n", $delta_ms, pid, comm, args->ret);
  }
  delete(@start[tid]);
}

tracepoint:syscalls:sys_enter_fdatasync /pid == @target_pid/
{
  @start_fd[tid] = nsecs;
}

tracepoint:syscalls:sys_exit_fdatasync /@start_fd[tid] && pid == @target_pid/
{
  $delta_ms = (nsecs - @start_fd[tid]) / 1000000;
  if ($delta_ms >= @threshold_ms) {
    printf("fdatasync %d ms pid=%d comm=%s ret=%d\n", $delta_ms, pid, comm, args->ret);
  }
  delete(@start_fd[tid]);
}

END
{
  clear(@start);
  clear(@start_fd);
  clear(@target_pid);
  clear(@threshold_ms);
}
